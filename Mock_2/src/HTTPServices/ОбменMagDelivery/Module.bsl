
#Область МетодыСервиса

Функция statusgetstatus(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	Ответ.Заголовки.Вставить("Content-Type", "application/json; charset=utf-8");
	
	ТелоОтвета = Новый Структура;
	ТелоОтвета.Вставить("status", "OKей");
	
	Ответ.УстановитьТелоИзСтроки(ИнструментыВебСервисов.ТекстJSON(ТелоОтвета), КодировкаТекста.UTF8);
	
	Возврат Ответ;

КонецФункции

Функция warehouse_to_customerPOST(Запрос)
	
	ИмяМетода = "warehouse_to_customer";
	
	КодУспешногоОтвета = ИнструментыВебСервисов.КодУспешногоОтвета();
	КодОшибкиЗапроса = ИнструментыВебСервисов.КодОшибкиЗапроса();
	КодОшибкиОбработки = ИнструментыВебСервисов.КодОшибкиОбработки();
	
	Ответ = Новый HTTPСервисОтвет(КодУспешногоОтвета);
	
	ТелоОтвета = Новый Структура;
	ТелоОтвета.Вставить("data", Неопределено);
	ТелоОтвета.Вставить("pagination", Неопределено);
	ТелоОтвета.Вставить("errors", Новый Массив);
	
	НастройкиОбмена = ЭмуляцияMagDelivery.НастройкиОбмена();

	ОписаниеВерсииАПИ = ЭмуляцияMagDelivery.ОписаниеВерсииАПИ(Запрос);

	Если Не ЭмуляцияMagDelivery.ПроверкаВерсииAPI(ОписаниеВерсииАПИ.ВерсияAPI, НастройкиОбмена.ТекущаяВерсияAPI) Тогда
		Ответ.КодСостояния = КодОшибкиЗапроса;
		ТекстОшибки = НСтр("ru='Запрашиваемая версия API неактивна. Обратитесь к администратору базы 1С.'");
		ТелоОтвета.errors.Добавить(ТекстОшибки);
	Иначе

		Если СтрНайти(ОписаниеВерсииАПИ.Методы, ИмяМетода) > 0 Тогда
			ЭмуляцияMagDelivery.ИмпортЗаказов(Запрос, Ответ, ТелоОтвета);
		Иначе
			Ответ.КодСостояния = КодОшибкиОбработки;
			ТекстШаблона = НСтр("ru='Для версии API %1 метод %2 не найден. Обратитесь к администратору базы 1С.'");
			ТекстОшибки = СтрШаблон(ТекстШаблона, НастройкиОбмена.ТекущаяВерсияAPI, ИмяМетода);
			ТелоОтвета.errors.Добавить(ТекстОшибки);
		КонецЕсли;

	КонецЕсли;
	
	ДанныеJSON = ИнструментыВебСервисов.ЗаписатьСтрокуJSON(ТелоОтвета);
	
	Ответ.Заголовки.Вставить("Content-type", "application/json");
	
	Ответ.УстановитьТелоИзСтроки(ДанныеJSON, КодировкаТекста.UTF8, ИспользованиеByteOrderMark.НеИспользовать);
	
	Возврат Ответ;
	
КонецФункции

#КонецОбласти