#Область ПрограммныйИнтерфейс

Функция ИменаРеквизитовОбъекта(Ссылка) Экспорт
	
	Результат = "";
	МетаданныеОбъекта = Ссылка.Метаданные();
	
	Для Каждого Реквизит Из МетаданныеОбъекта.Реквизиты Цикл
		Результат = Результат + ?(ПустаяСтрока(Результат), "", ", ") + Реквизит.Имя;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

// Выполняет преобразование формата YYYY-MM-DDThh:mm:ss±hh:mm
// в дату.
//
// Параметры:
//  Значение - Строка - значение которое необходимо преобразовать.
//
// Возвращаемое значение:
//  Дата - результат преобразования.
//
Функция ПривестиЗначениеКДате(Знач ЗначениеДата) Экспорт
	
	Если Не ЗначениеЗаполнено(ЗначениеДата) Тогда
		Возврат Дата(1,1,1);
	КонецЕсли;
	
	// Часовой пояс игнорируется.
	Если СтрДлина(ЗначениеДата) > 5 Тогда
		ПозицияНачалаЧасовогоПояса = СтрДлина(ЗначениеДата) - 5;
		НачалоЧасовогоПояса = Сред(ЗначениеДата, ПозицияНачалаЧасовогоПояса, 1);
		Если (НачалоЧасовогоПояса = "+" ИЛИ НачалоЧасовогоПояса = "-")
			И Сред(ЗначениеДата, ПозицияНачалаЧасовогоПояса + 3, 1) = ":" Тогда
	
			ЗначениеДата = Лев(ЗначениеДата, СтрДлина(ЗначениеДата) - 6);
			
		КонецЕсли;
	КонецЕсли;
	
	ЗначениеДата = СокрЛП(СтрЗаменить(ЗначениеДата, ".", ""));
	ЗначениеДата = СокрЛП(СтрЗаменить(ЗначениеДата, "-", ""));
	ЗначениеДата = СокрЛП(СтрЗаменить(ЗначениеДата, ":", ""));
	ЗначениеДата = СокрЛП(СтрЗаменить(ЗначениеДата, "T", ""));
	
	ОписаниеТипа = Новый ОписаниеТипов("Дата");
	Результат = ОписаниеТипа.ПривестиЗначение(ЗначениеДата);
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Результат = ОписаниеТипа.ПривестиЗначение(Лев(ЗначениеДата, 8)); // только дата "ГГГГММДД"
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти