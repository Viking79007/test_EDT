
#Область ОбработчикиСобытийф

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Элементы.ЧасовойПояс.СписокВыбора.ЗагрузитьЗначения(ПолучитьДопустимыеЧасовыеПояса());

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтправитьЗаказВТестируемуюБазу(Команда)
	
	ПараметрыОтправки = ПараметрыОтправкиДанныхВТестируемуюБазу();
	ПараметрыОтправки.ОтправлятьЗаказ = Истина;
	ЗаписатьДокументИОтправитьДанные(ПараметрыОтправки);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьОплатуВТестируемуюБазу(Команда)

	ПараметрыОтправки = ПараметрыОтправкиДанныхВТестируемуюБазу();
	ПараметрыОтправки.ОтправлятьОплату = Истина;
	ЗаписатьДокументИОтправитьДанные(ПараметрыОтправки);

КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗаказИОплатуВТестируемуюБазу(Команда)
	
	ПараметрыОтправки = ПараметрыОтправкиДанныхВТестируемуюБазу();
	ПараметрыОтправки.ОтправлятьОплату = Истина;
	ПараметрыОтправки.ОтправлятьЗаказ = Истина;
	ЗаписатьДокументИОтправитьДанные(ПараметрыОтправки);

КонецПроцедуры

&НаКлиенте
Процедура ОтправитьВозвратОплатыВТестируемуюБазу(Команда)
	
	ПараметрыОтправки = ПараметрыОтправкиДанныхВТестируемуюБазу();
	ПараметрыОтправки.ОтправлятьВозвратОплаты = Истина;
	ЗаписатьДокументИОтправитьДанные(ПараметрыОтправки);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)

	ПересчитатьСуммуВТекущейСтрокеТоваров();

КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоБазовыхЕдиницПриИзменении(Элемент)
	
	ПересчитатьСуммуВТекущейСтрокеТоваров();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Асинх Процедура ЗаписатьДокументИОтправитьДанные(ПараметрыОтправки)
	
	Если Модифицированность Тогда

		ТекстВопроса = НСтр("ru = 'Для продолжения операции необходимо записать документ.
							|Записать документ?'");

		Обещание = ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
		Результат = Ждать Обещание;

		Если Результат = КодВозвратаДиалога.Да Тогда
			Если Записать() Тогда
				ОтправитьДанныеВТестируемуюБазуНаСервере(ПараметрыОтправки);
			КонецЕсли;
		КонецЕсли;

	Иначе
		ОтправитьДанныеВТестируемуюБазуНаСервере(ПараметрыОтправки);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтправитьДанныеВТестируемуюБазуНаСервере(ПараметрыОтправки)

	ЭмуляцияИнтернетМагазина.ОтправитьЗаказВТестируемуюБазу(Объект.Ссылка, ПараметрыОтправки);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПараметрыОтправкиДанныхВТестируемуюБазу()
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("ОтправлятьЗаказ", Ложь);
	ПараметрыОтправки.Вставить("ОтправлятьОплату", Ложь);
	ПараметрыОтправки.Вставить("ОтправлятьВозвратОплаты", Ложь);
	
	Возврат ПараметрыОтправки;
	
КонецФункции

&НаКлиенте
Процедура ПересчитатьСуммуВТекущейСтрокеТоваров()

	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.КоличествоБазовыхЕдиниц;

КонецПроцедуры

#КонецОбласти