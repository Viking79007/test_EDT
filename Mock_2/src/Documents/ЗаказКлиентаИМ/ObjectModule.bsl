#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)

	Если ДанныеЗаполнения = Неопределено Тогда
		
		ЗаполнитьРеквизитыПоУмолчанию();
	
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
	
		Сумма = ДанныеЗаполнения.total;
		ДатаНачалаДоставки = КДВ_ОбщегоНазначения.ПривестиЗначениеКДате(ДанныеЗаполнения.delivery.date.from);
		ДатаОкончанияДоставки = КДВ_ОбщегоНазначения.ПривестиЗначениеКДате(ДанныеЗаполнения.delivery.date.to);
		
		Товары.Очистить();
		Для Каждого Товар Из ДанныеЗаполнения.items Цикл
			
			НоваяСтрока = Товары.Добавить();
			НоваяСтрока.Номенклатура = Справочники.Номенклатура.НайтиПоРеквизиту("guid", Товар.product);
			НоваяСтрока.Цена = Товар.price;
			НоваяСтрока.Количество = Товар.amount;
			НоваяСтрока.Сумма = Товар.total;
			НоваяСтрока.КоличествоБазовыхЕдиниц = Товар.quantity;
			
		КонецЦикла;
	
	КонецЕсли;

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ЗаполнитьРеквизитыПоУмолчанию();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьРеквизитыПоУмолчанию()

	guid = Строка(Новый УникальныйИдентификатор);
	ДатаЗаказа = ТекущаяДатаСеанса();
	ДатаНачалаДоставки = ДатаЗаказа;
	ДатаОкончанияДоставки = ДатаЗаказа + 60 * 60 * 4;
	ДатаОплаты = ДатаЗаказа;
	Статус = Перечисления.СтатусыЗаказовИМ.Новый;
	Источник = "web-desktop";

КонецПроцедуры

#КонецОбласти

#КонецЕсли